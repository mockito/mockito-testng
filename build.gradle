buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "io.github.gradle-nexus:publish-plugin:2.0.0"
        classpath 'org.shipkit:shipkit-changelog:2.0.1'
        classpath 'org.shipkit:shipkit-auto-version:2.1.2'
    }
}

description = "Mockito for TestNG"

apply plugin: "java-library"

apply from: "gradle/reproducible.gradle"
apply from: "gradle/shipkit.gradle"
apply from: "gradle/java-publication.gradle"
apply from: "gradle/ide.gradle"

group = "org.mockito"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal() //useful for testing when consuming locally built dependencies (like Mockito)
    mavenCentral()
}

dependencies {
    def mockitoVersion = "5.2.0"

    api "org.mockito:mockito-core:$mockitoVersion"
    api "org.testng:testng:7.11.0"

    testImplementation "org.assertj:assertj-core:3.27.6"
    testImplementation "org.mockito:mockito-inline:$mockitoVersion"
}

test {
    include "**/*Test.class"
    useTestNG()
    testLogging {
        exceptionFormat 'full'
        showCauses true
    }
}
